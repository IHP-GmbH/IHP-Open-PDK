*#######################################################################
*
* Copyright 2023 IHP PDK Authors
* 
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
* 
*    https://www.apache.org/licenses/LICENSE-2.0
* 
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*
*#######################################################################
       
.MODEL darea D (tnom=27 is=2.315E-019 n=1.009 ik=1.434E-007 rs=2.193E+005 cj0=9.371E-016 m=0.3036 vj=0.696 fc=0.5 bv=11.64 ibv=3.458E-007 tcv=0.001 nbv = 48.9 isr=7.413E-019 xti=5.039 trs=0 trs2=0 tt=7.009E-007)
.MODEL dperim D (tnom=27 is=3.851E-021 n=1.022 rs=1.688E+006 ik=7.838E-008 isr=1.011E-017 xti=3 bv=31.86 ibv=6.115E-014 nbv=112.1 cj=1.821E-017 m=0.01923 vj=0.1306 tt=7.009E-007)
.MODEL dcorner D (tnom=27 is=6.502E-021 n=1 rs=4.857E+005 ik=1E-006 isr=0 xti=3 bv=11.64 ibv=3.458E-007 cj=0 m =0.5 vj=0.7)
.MODEL dparea D (tnom=27 is=8.569E-020 n=1.003 ik=2.957E-007 rs=1.728E+005 cj=8.252E-016 m=0.3171 vj=0.6628 fc=0.5 bv=12.14 ibv=1E-006 tcv=0.001 nbv=11.85 isr=2.421E-019 xti=4.56 trs=0 trs2=0 tt=1E-012)
.MODEL dpperim D (tnom=27 is=1.186E-020 n=1.006 rs=1.577E+006 ik=2.158E-008 isr=7.211E-018 xti = 4.118 bv=27.26 ibv=1.832E-011 nbv=32.47 cj=3.657E-017 m=0.1512 vj=0.5241 tt=1E-012)
.MODEL dpcorner D (tnom=27 is=6.965E-021 n=1 rs=4.734E+005 ik=1.19E-008 isr=0 xti=3 cj=0 m=0.5 vj=0.7)

**************************************
* Device: dantenna
* Testfield: T262
* Technology: IHP SG13S technologies
* Lot: AWL403#A
* Wafer: 19
* Chip: 4,7
* Segment: S350B
* DUTs: D1 (area), D2 (perimeter) and D4 (corners)
* Temperature range: -40째C ... +125째C
* DC range: -15V ... 1V
* Operator: Korndoerfer
* Date: 05.03.2013
* 24.03.2015 WW area dependency and and temperature dependency 
* for dantenna and dpantenna break down updated
* Remarks: model is designed for reverse direction of the diodes
*          NO statistics
*          initial model
*  DEV_A = area of the diode in um*umnew
*  DEV_P = perimeter of the diode in um
*  DEV_C = number of corners (default=4)
*************************************

.subckt dantenna 1 2
*subckt dantenna (bn C)
*                (anode  cathode)
*      (substrate/pWell  NSD)
.params l=780n   w=780n   DEV_A=(l*w)/1p  DEV_P=(l+w)/0.5u  DEV_C=4
D1 1 2 darea area=DEV_A
D2 1 2 dperim area=DEV_P
D3 1 2 dcorner area=DEV_C
.ends dantenna

**************************************
* Device: dpantenna
* Testfield: T262
* Technology: IHP SG13S technologies
* Lot: AWL403#A
* Wafer: 19
* Chip: 4,7
* Segment: S351B
* DUTs: D1 (area), D2 (perimeter) and D4 (corners)
* Temperature range: -40째C ... +125째C
* DC range: -15V ... 1V
* Operator: Korndoerfer
* Date: 05.03.2013
* Remarks: model is designed for reverse direction of the diodes
*          bipolar effect in forward direction is NOT modeled
*          NO statistics
*          initial model
*  DEV_A = area of the diode in um*um
*  DEV_P = perimeter of the diode in um
*  DEV_C = number of corners (default=4)
*************************************

.subckt dpantenna 1 2
*              (PSD  nWell)
*             (anode  cathode)
.params l=780n  w=780n  DEV_A=(l*w)/1p  DEV_P=(l+w)/0.5u  DEV_C=4
D1 1 2 dparea area=DEV_A
D2 1 2 dpperim area=DEV_P
D3 1 2 dpcorner area=DEV_C
.ends dpantenna

